' Gambas class file

'
' tradukisto
' Translation of software
'
' Copyright (C) Martín Belmonte
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
'

Export

'' Is like Version but its return a float number 315.2 316.0

Static Public Function Edition() As Float

  Dim sVersion As String
  Dim aVersion As String[]
  Dim fVersion As Float

  sVersion = System.FullVersion

  If InStr(sVersion, ".") Then
    aVersion = Split(sVersion, ".")
    Select aVersion.Count
      Case 2
        fVersion = CFloat(aVersion[0] & aVersion[1] & "." & "0")
      Case 3
        fVersion = CFloat(aVersion[0] & aVersion[1] & "." & aVersion[2])
    End Select

  Endif
  Return fVersion

End

'' Returns the default file manager.

Public Function DeFiBro() As String

  Dim str As String

  Shell "xdg-mime query default inode/directory" To str

  Return str

End

'' Returns the xmi installed distribution

Public Function DistroNoshell() As String

  Dim str As String

  str = File.Load("/etc/issue.net")
  str = Replace(str, "\n", "")

  Return str

End

'' Return the distro

Static Public Function Distro() As String

  Dim strDistro As String
  Dim strDistroShort As String
  Dim stxDistros As New String[]
  Dim stxDistro As New String[]
  Dim intN As Integer

  strDistro = File.Load("/etc/issue")
  Replace(strDistro, gb.Tab, "")
  strDistro = Trim(strDistro)

  strDistro = strDistro
  strDistro = Replace(strDistro, "  ", " ")
  strDistro = Replace(strDistro, "  ", " ")
  strDistro = Replace(strDistro, gb.Tab, " ")
  strDistro = Replace(strDistro, gb.Tab, " ")
  strDistro = String.LCase(strDistro)

  stxDistros.clear
  stxDistro.clear

  stxDistros.Add("arch")
  stxDistros.Add("debian")
  stxDistros.Add("fedora")
  stxDistros.Add("redhat")
  stxDistros.Add("gentoo")
  stxDistros.Add("manjaro")
  stxDistros.Add("mint")
  stxDistros.Add("opensuse")
  stxDistros.Add("ubuntu")
  stxDistros.Add("peppermint")

  stxDistro = Split(strDistro, " ")

  strDistroShort = ("Unknown")

  For intN = 0 To stxDistro.Max

    If stxDistros.Find(stxDistro[intN]) <> -1 Then
      strDistroShort = stxDistro[intN]
      Break
    Endif

  Next

  Return strDistroShort

End

''Returns the operating system architecture

Public Function ArqSO() As String

  Return System.Architecture

End

''Returns the Processor Architecture

Public Function ArqMicro() As String

  Dim sArqPro As String

  Exec ["uname", "-p"] To sArqPro
  Return Replace(sArqPro, "\n", "")

End

'' Returns the type of Processor

Public Function MicroType() As String

  Dim sPro As String

  Shell "cat /proc/cpuinfo | grep -i ghz | uniq | cut -f2 -d" & ":" To sPro
  Return LTrim(Replace(sPro, "\n", ""))

End

'' Returns amount of Ram in MB also with free -m

Public Function Ram() As String

  Dim sRam As String

  Exec ["cat", "/proc/meminfo"] To sRam
  Try sRam = Scan(sRam, "*MemTotal:*\n*")[1]
  If Error Then
    sRam = ""
  Else
    While Not IsDigit(Right(sRam))
      sRam = Left(sRam, -1)
    Wend
    sRam = Trim(sRam)
  Endif

  If sRam Then
    Try sRam = Format(CInt(sRam) / 1024, "0") & " MB"
    If Not Error Then Return sRam
  Endif

End

'' Returns the computer name

Public Function ComputerName() As String

  Return System.Host

End

'' Returns the active user
Public Function CurrentUser() As String[]

  Return System.User.Name

End

'' Returns the a list of Linux system users.

Public Function GetSystemUsers() As String[]

  Dim stxTUsr As New String[]
  Dim stxUsr As New String[]
  Dim strUser As String

  stxTUsr = File.Load2("/etc/passwd")

  If stxTUsr.Count > 0 Then
    For Each strUser In stxTUsr
      If Split(strUser, ":")[5] = "/home" &/ Split(strUser, ":")[0] Then
        If InStr(Split(strUser, ":")[6], "false") = 0 Then
          stxUsr.Add(Split(strUser, ":")[0])
        Endif
      Endif
    Next
  Endif

  Return stxUsr

End

'' Returns the users we have created

Public Function AllUsers() As String

  Dim sUser As String

  Shell "cat /etc/passwd |grep -iw '100.' | cut -d ':' -f1" To sUser

  Return Replace(sUser, "\n", "")

End

'' Returns the Computer Workgroup

Public Function WGroup() As String

  Return System.Domain

End

'' Returns the filesystem

Public Function SysFile(ruta As String) As String

  Dim sSF As String

  Exec ["df", "-h", ruta] To sSF 'We store characteristics of the File System passed en route
  Try sSF = Scan(sSF, "*\n*")[1] 'We hide the Error if it can't find the string
  If Error Then sSF = ""
  Return Replace(sSF, "\n", "")

End

'' Returns the gambas version installed on the computer

Public Function Vgambas() As String

  Return System.FullVersion

End

'' Returns the IP of the computer, it only works under OS with Systemd, for this it uses shell and the command "ip addr show".

Public Function AddressIP() As String

  Dim strAddr As String
  Dim stxAddr As New String[]
  Dim strPart As String
  Dim strIP As String

  Shell "ip addr show" To strAddr
  If Len(strAddr) > 0 Then
    stxAddr = Split(strAddr, " ")
    For Each strPart In stxAddr
      If InStr(strPart, ".") > 0 Then
        If InStr(strPart, "/24") > 0 Then
          strIP = Split(strPart, "/")[0]
        Endif
      Endif
    Next
  Endif
  Return strIP

End

'' Returns the IP v4 of the local network, as input parameter requires the base IP address, for example "192.168.1" but if the parameter is not passed then use the IP of the computer where the program is running removing the last one number. The output format of each item in the array is host-name [tab] 8.8.8.8

Public Function LANIP(Optional strBase As String) As String[]

  Dim strAddr As String
  Dim stxTmp As New String[]
  Dim stxAddr As New String[]
  Dim strPart As String

  If InStr(strBase, ".") = 0 Then
    strBase = Split(AddressIP(), ".")[0] & "."
    strBase &= Split(AddressIP(), ".")[1] & "."
    strBase &= Split(AddressIP(), ".")[2]
  Endif

  Shell "nmap -sP " & strBase & ".1-254" To strAddr

  If Len(strAddr) > 0 Then
    stxTmp = Split(strAddr, "\n")
    For Each strPart In stxTmp
      If InStr(strPart, "Nmap scan report for ") > 0 Then
        strPart = Replace(strPart, "Nmap scan report for ", "")
        strPart = Replace(strPart, " ", "\t")
        strPart = Replace(strPart, "(", "")
        strPart = Replace(strPart, ")", "")
        stxAddr.Add(strPart)
      Endif
    Next
  Endif
  Return stxAddr

End

'' Returns the last digit of the IP

Public Function LastNIP() As String

  Dim sIPs As String

  Shell "ifconfig | grep inet: | grep Difus.|cut -d '.' -f5|cut  -d ' ' -f1" To sIPs
  Return RTrim(Replace(sIPs, "\n", " "))

End

'' Returns the serial number of the hard disk to be used as Pk in the BDD

Public Function Hdserial() As String

  Dim sNS As String

  Shell "udevadm info --query=property --name=sda | grep SERIAL_ | awk -F '=' '{print $2}'" To sNS
  Return RTrim(Replace(sNS, "\n", " "))

End

'' Returns UUID of the swap to be used as PK of the BDD (Requires Administrator permissions)

Public Function UUIDswap() As String

  Dim sUID As String

  Shell "blkid | grep swap | cut -d ' ' -f2 |cut -d '=' -f2" To sUID
  Return Left(Right(sUID, -1), -2)

End

''Returns the Date of the last time the system was updated

Public Function LastUpgrade() As String

  Dim long_iso As String
  Dim strDistroShort As String
  Dim strLastUpgrade As String

  long_iso = "long-iso"

  strDistroShort = Distro()

  Select strDistroShort
    Case "debian"
      strLastUpgrade = Format$(Stat("/var/log/apt").Time, "yyyy/mm/dd hh:nn:ss")
      strLastUpgrade = RTrim(Replace(strLastUpgrade, "\n", " "))

    Case Else
      strLastUpgrade = ("Unknown")
  End Select

  Return strLastUpgrade

End

'' <p>Returns a text matrix with the configuration of the system number.</p><ol><li>Decimales</li><li>Miles</li><li>??</li><li>??</li><li>??</li><li>Codificación</li></ol>

Public Function GetLcNumeric() As String[]

  Dim str As String
  Dim stx As New String[]

  Shell "locale LC_NUMERIC" To str

  If InStr(str, "\n") > 0 Then
    stx = Split(str, "\n")
  Endif

  Return stx

End

''Returns the status regarding the installation of a package. It requires as an input parameter the exact name of the package.

Static Public Function PkgStat(strPkg As String) As Boolean

  Dim strDistroShort As String
  Dim strCommand As String
  Dim strPkgStatus As String
  Dim bolPkgStatus As Boolean

  strDistroShort = Distro()

  ' Obtencion de resultados
  Select strDistroShort
    Case "arch", "manjaro"
      strCommand = "pacman -Qs " & strPkg

      Shell strCommand & " 2>&1" To strPkgStatus

      If strPkgStatus <> "" Then
        strPkgStatus = Split(strPkgStatus, "\n")[0]
        strPkgStatus = Split(strPkgStatus, " ")[0]
      Endif

      Select strPkgStatus
        Case "local/" & strPkg
          bolPkgStatus = True
        Case Else
          bolPkgStatus = False
      End Select

    Case "debian", "ubuntu", "mint", "peppermint"

      strCommand = "dpkg-query -W -f='${Status}\n' " & strPkg

      Shell strCommand & " 2>&1" To strPkgStatus

      If strPkgStatus = "install ok installed\n" Then
        bolPkgStatus = True
      Else
        bolPkgStatus = False
      Endif

  End Select

  Return bolPkgStatus

End

''Returns a text matrix with the paths of the music folders, images, documents, etc. the /home/user directory

Public Function XDGFolder() As String[]

  Dim strHome As String
  Dim stxXDGTmp As New String[]
  Dim stxXDGFolders As New String[]
  Dim strDirsFile As String
  Dim strFlux As String
  Dim intL As Integer
  Dim strTmp As String
  Dim strVar As String
  Dim strFolder As String

  strHome = User.Home

  strDirsFile = strHome &/ ".config/user-dirs.dirs"

  If Exist(strDirsFile) Then
    strFlux = File.Load(strDirsFile)

    stxXDGTmp = Split(strFlux, gb.NewLine)

    For intL = 0 To stxXDGTmp.Max
      strTmp = stxXDGTmp[intL]
      If strTmp <> "" Then
        If String.Mid(strTmp, 1, 4) = "XDG_" Then
          If InStr(strTmp, "=")

            strVar = ""
            strFolder = ""

            strVar = Split(strTmp, "=")[0]
            strVar = Split(strVar, "_")[1]
            strVar = String.LCase(strVar)

            strFolder = Split(strTmp, "=")[1]
            strFolder = Replace(strFolder, "$HOME/", "")
            strFolder = Replace(strFolder, Chr(34), "")
            strFolder = strHome &/ strFolder

            stxXDGFolders.Add(strVar & ":" & strFolder)

          Endif
        Endif
      Endif
    Next
  Endif

  Return stxXDGFolders

End

''Check if the packages that are passed to you as parameters in an array are installed in the system, it returns an array with the packages that are not installed, if everything was, the returned array would be empty.

Public Function PkgDep(stxPackages As String[]) As String[]

  Dim intPkg As Integer
  Dim stxMissing As New String[] ' Lista de paquetes que falta instalar

  For intPkg = 0 To stxPackages.Max
    If PkgStat(stxPackages[intPkg]) = False Then
      stxMissing.Add(stxPackages[intPkg])
    Endif
  Next

  Return stxMissing

End

Public Sub Install_read()

  Dim StrInput As String

  Read #Last, StrInput, -80

End

''Returns a list of all the mime types that are covered in the system.

Public Function MimeTypesList() As String[]

  Dim stxOut As New String[]
  Dim strDefaults As String

  strDefaults = "/usr/share/mime/types"

  If Exist(strDefaults) Then
    If Stat(strDefaults).Type = gb.File Then
      stxOut = Split(File.Load(strDefaults), "\n")
    Endif
  Endif

  Return stxOut

End

''Returns a list with the default associated program and the path of the icon if it exists.

Static Public Function MimeDefaultApp(strMimeType As String) As String[]

  Dim stx As New String[]
  Dim strOut As String
  Dim stxOut As New String[]
  Dim strDefaults As String
  Dim strLine As String
  Dim strMim As String
  Dim strApp As String

  If Exist("/usr/share/applications/defaults.list") Then
    strDefaults = "/usr/share/applications/defaults.list"
  Else
    If Exist(User.Home &/ ".config/mimeapps.list") Then
      strDefaults = User.Home &/ ".config/mimeapps.list"
    Endif
  Endif

  If Exist(strDefaults) Then
    Select Stat(strDefaults).Type
      Case gb.File, gb.Link
        stx = Split(File.Load(strDefaults), "\n")
        If stx.Count > 0 Then
          For Each strLine In stx
            If InStr(strLine, "=") > 0 Then
              strLine = Replace(strLine, "org.gnome.", "")
              strLine = Replace(strLine, ".Desktop", "")
              'strLine = CleanDestop(strLine)
              strMim = Split(strLine, "=")[0]
              strApp = Split(strLine, "=")[1]
              If strMim = strMimeType Then
                If InStr(strApp, ".") > 0 Then
                  strOut = Split(strApp, ".")[0]
                Else
                  strOut = strApp
                Endif
                stxOut.Add(strOut)
              Endif
            Endif

          Next
        Endif
    End Select
  Endif

  Return stxOut

End

' Private Function CleanDestop(s As String) As String
'
'   Dim stx As New String[]
'   Dim r As String
'
'   stx = ["org.gnome.", ".Desktop"]
'   For Each r In stx
'     s = Replace(s, r, "")
'   Next
'
'   Return s
'
' End

'' Returns an identifier of length N generated by the random combination of characters and numbers. It is for lists of objects that you want to name with names of 4 or 6 characters for example.

Public Function UUIDHandle(i As Integer) As String

  Dim s As String
  Dim x As Integer
  Dim stx As New String[]
  Dim o As String
  Dim c As Integer
  Dim r As Integer

  o = ""

  If i > 0 Then

    s = "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ"

    For x = 1 To String.Len(s)
      stx.Add(String.Mid(s, x, 1))
    Next

    For c = 0 To i - 1
      r = Rand(0, stx.Count - 1)
      o &= stx[r]
    Next

  Endif

  Return o

End

''Returns a Universal Unique Identifier generated by the <b> uuidgen -t </b> command. Just change the octet of the timestamp. Remember that it is generated based on the time "-t"

Public Function UUIDGen() As String

  Dim str As String

  Shell "uuidgen -t" To str

  If String.Len(str) > 36 Then
    str = String.Mid(str, 1, 36)
  Endif

  Return str

End

''Returns a universal unique identifier generated by loading the /proc/sys/kernel/random/uuid file. It always gives all the different octets for each time the function is run.

Public Function UUIDProc() As String

  Dim str As String

  str = File.Load("/proc/sys/kernel/random/uuid")
  str = RTrim(str)

  Return str

End

'' Create a list with all the gambas components

Public Function GambasComponents() As Collection

  Dim out As New Collection

  out.add([("Internal native classes "), True], "gb")
  out.add([("Program arguments parser "), True], "args")
  out.add([("More container datatypes "), True], "data")
  out.add([("Debugger helper "), True], "debug")
  out.add([("Expression evaluator "), True], "eval")
  out.add([("Syntax highlighter routines "), True], "eval.highlight")
  out.add([("Filesystem events monitoring "), True], "inotify")
  out.add([("Flexible log message handling "), True], "logging")
  out.add([("NCurses library "), True], "ncurses")
  out.add([("GNU-style command-line arguments parser "), True], "option")
  out.add([("Perl Compatible Regular Expression matching "), True], "pcre")
  out.add([("Application settings management "), True], "settings")
  out.add([("POSIX signals management "), True], "signal")
  out.add([("Terminal management "), True], "term")
  out.add([("Unit test framework "), True], "test")
  out.add([("Miscellaneous utility functions "), True], "util")
  out.add([("Visual-Basic compatibility "), True], "vb")
  out.add([("D-Bus interface "), True], "dbus")
  out.add([("QT4/QT5 WebKit switcher component "), True], "gui.qt.webkit")
  out.add([("Online map viewing component "), True], "map")
  out.add([("Gambas Markdown markup syntax "), True], "markdown")
  out.add([("Mime message management "), True], "mime")
  out.add([("Network access and serial port management "), True], "net")
  out.add([("Network high-level protocol management based on the Curl library "), True], "net.curl")
  out.add([("POP3 client "), True], "net.pop3")
  out.add([("SMTP client "), True], "net.smtp")
  out.add([("Web browser component based on WebKit for gb.qt4 "), True], "qt4.webkit")
  out.add([("Web browser component based on WebKit for gb.qt5 "), True], "qt5.webkit")
  out.add([("Miscellaneous utility functions for web applications "), True], "util.web")
  out.add([("Web applications development "), True], "web")
  out.add([("Web feed parser and generator "), True], "web.feed")
  out.add([("Web applications GUI component using processes as session "), True], "web.gui")
  out.add([("Web applications GUI component "), True], "web.form")
  out.add([("Database access "), True], "db")
  out.add([("Data bound controls "), True], "db.form")
  out.add([("MySQL specific routines "), True], "mysql")
  out.add([("Memcached client "), True], "memcached")
  out.add([("Old XML tools based on libxml "), True], "libxml")
  out.add([("XML tools now made in Gambas "), True], "xml")
  out.add([("HTML generator "), True], "xml.html")
  out.add([("XSLT tools based on libxml "), True], "xml.xslt")
  out.add([("XML-RPC protocol implementation "), True], "xml.rpc")
  out.add([("Compression library "), True], "compress")
  out.add([("MD5/DES crypting "), True], "crypt")
  out.add([("OpenSSL library routines "), True], "openssl")
  out.add([("Enhanced controls "), True], "form")
  out.add([("Enhanced version of the standard dialogs "), True], "form.dialog")
  out.add([("Multi-document interface "), True], "form.mdi")
  out.add([("Enhanced text editor with syntax highlighting "), True], "form.editor")
  out.add([("Terminal emulator "), True], "form.terminal")
  out.add([("Generic preview and print form "), True], "form.print")
  out.add([("GTK+2 toolkit implementation "), True], "gtk")
  out.add([("GTK+3 toolkit implementation "), True], "gtk3")
  out.add([("Qt/GTK+ component switcher "), True], "gui")
  out.add([("Qt4/Qt5 component switcher "), True], "gui.qt")
  out.add([("PDF renderer based on Poppler "), True], "pdf")
  out.add([("Qt4 toolkit implementation "), True], "qt4")
  out.add([("Qt4 toolkit extension "), True], "qt4.ext")
  out.add([("Qt5 toolkit implementation "), True], "qt5")
  out.add([("Report designer "), True], "report")
  out.add([("Report designer "), True], "report2")
  out.add([("Vectorial drawing based on Cairo "), True], "cairo")
  out.add([("Polygon clipping library based on the Clipper library "), True], "clipper")
  out.add([("Image management component "), True], "image")
  out.add([("Image routines based on the Imlib2 library "), True], "image.imlib")
  out.add([("Image loading and saving "), True], "image.io")
  out.add([("Image filtering routines "), True], "image.effect")
  out.add([("OpenGL for GTK+2 toolkit "), True], "gtk.opengl")
  out.add([("OpenGL with QT/GTK+ switcher component "), True], "gui.opengl")
  out.add([("OpenGL library interface "), True], "opengl")
  out.add([("OpenGL Library Utility interface "), True], "opengl.glu")
  out.add([("OpenGL Shaders Library interface "), True], "opengl.glsl")
  out.add([("OpenGL Simple Game Engine based on MD2 format "), True], "opengl.sge")
  out.add([("OpenGL for QT4 toolkit "), True], "qt4.opengl")
  out.add([("OpenGL for QT5 toolkit "), True], "qt5.opengl")
  out.add([("Desktop management component "), True], "desktop")
  out.add([("Gnome keyring support for desktop management component "), True], "desktop.gnome.keyring")
  out.add([("X-Window library interface "), True], "desktop.x11")
  out.add([("System tray icon management "), True], "gui.trayicon")
  out.add([("SDL 1.2 library interface "), True], "sdl")
  out.add([("SDL 1.2 Sound library interface "), True], "sdl.sound")
  out.add([("SDL 2 component interface "), True], "sdl2")
  out.add([("SDL 2 audio component interface "), True], "sdl2.audio")
  out.add([("GStreamer library interface "), True], "media")
  out.add([("Multimedia player control. "), True], "media.form")
  out.add([("OpenAL library interface "), True], "openal")
  out.add([("Scanner management library "), True], "scanner")
  out.add([("Video capture based on Video4Linux "), True], "v4l")
  out.add([("Complex number implementation "), True], "complex")
  out.add([("Gnu Multiple Precision Arithmetic Library interface "), True], "gmp")
  out.add([("Gnu Scientific Library interface "), True], "gsl")

  Return out

End
